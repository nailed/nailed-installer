apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

group = 'jk_5.nailed'
archivesBaseName = 'installer'
version = '1.0'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

configurations {
    deployerJars
}

dependencies {
    compile 'com.google.guava:guava:14.0'
    compile 'com.google.code.gson:gson:2.2.4'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

task fatJar(type: Jar, dependsOn: jar) {
    inputs.file jar.archivePath
    from(configurations.compile.collect {
        zipTree(it).matching {
            include '**/*.class'
        }
    })
    doFirst {
        from zipTree(jar.archivePath)
        manifest {
            attributes 'Main-Class': 'jk_5.nailed.installer.SimpleInstaller'
        }
    }
    doLast {
        ant.signjar(jar: fatJar.archivePath, alias: 'Nailed', keystore: nailedKeystoreLocation, storepass: nailedKeystorePassword)
    }
}

artifacts {
    archives fatJar
}

jar {
    classifier = 'thin'
    manifest {
        attributes 'Main-Class': 'jk_5.nailed.installer.SimpleInstaller'
    }
    doLast {
        ant.signjar(jar: jar.archivePath, alias: 'Nailed', keystore: nailedKeystoreLocation, storepass: nailedKeystorePassword)
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "scp://maven.reening.nl/media/maven/") {
            authentication(userName: mavenUsername, password: mavenPassword)
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
