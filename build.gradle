apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:4.10'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://s3.amazonaws.com/Minecraft.Download/libraries"
    }
    maven {
        url "http://maven.reening.nl/"
    }
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}

group = 'jk_5.nailed'
archivesBaseName = 'nailed-installer'
version = '1.0'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

dependencies {
    compile 'jk_5.jsonlibrary:jsonlibrary:0.1-SNAPSHOT'
    compile 'com.google.guava:guava:14.0'
    compile 'net.sourceforge.argo:argo:3.7'
    compile 'org.tukaani:xz:1.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar, dependsOn: jar) {
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { include '**/*.class' } })
}

fatJar.doFirst {
    from zipTree(jar.archivePath)
}

task shrinkJar(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    inputs.file fatJar.archivePath
    ext {
        outDir = file("${buildDir}/proguard")
        obfuscatedJar = "${outDir}/${jar.baseName}.jar"
    }
    outDir.mkdirs()

    injars jar.archivePath
    outjars obfuscatedJar
    libraryjars configurations.compile
    configuration 'proguard.pro'
}

task shrinkOutput(type: Jar, dependsOn: shrinkJar) {
    classifier = 'shrunk'
    from zipTree(shrinkJar.obfuscatedJar)
    manifest {
        attributes 'Main-Class': 'jk_5.nailed.installer.SimpleInstaller'
    }
}

artifacts {
    archives fatJar
    archives sourcesJar
    archives shrinkOutput
}

jar {
    classifier = 'thin'
    exclude 'argo/**'
    manifest {
        attributes 'Main-Class': 'jk_5.nailed.installer.SimpleInstaller'
    }
}

uploadArchives {
    repositories {
        for (f in rootProject.repositories){
            if (f.name.equals('filesrepo')) {
                logger.info('Publishing to files server')
                add f
            }
        }
        logger.info('Publishing to repo folder')
        add project.repositories.fileRepo
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
