apply plugin: 'maven'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

group = 'jk_5.nailed'
archivesBaseName = 'Nailed-Installer'
version = '1.3'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

configurations {
    deployerJars
}

dependencies {
    compile 'org.scala-lang:scala-library:2.10.4'
    compile 'com.google.code.gson:gson:2.2.4'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

jar {
    from(configurations.compile.collect {
        zipTree(it).matching {
            include '**/*.class'
        }
    })
    manifest {
        attributes 'Main-Class': 'jk_5.nailed.installer.SimpleInstaller'
    }
    if(project.hasProperty("nailedKeystoreLocation")){
        doLast {
            logger.lifecycle("Signing jar")
            ant.signjar(jar: jar.archivePath, alias: 'Nailed', keystore: nailedKeystoreLocation, storepass: nailedKeystorePassword)
        }
    }
}

uploadArchives {
    if (project.hasProperty("deploymaven")) {
        logger.lifecycle('Deploying to file server')
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: project.deploymaven.url) {
                authentication(userName: project.deploymaven.username, password: project.deploymaven.password)
            }
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name = project.archivesBaseName
                    packaging = 'jar'
                    description = 'Installer for Nailed client profiles'
                    url = 'https://github.com/nailed/nailed-installer'

                    scm {
                        url 'https://github.com/nailed/nailed-installer'
                        connection 'scm:git:git://github.com/nailed/nailed-installer.git'
                        developerConnection 'scm:git:git@github.com:nailed/nailed-installer.git'
                    }

                    issueManagement {
                        system 'github'
                        url 'https://github.com/nailed/nailed-installer/issues'
                    }

                    developers {
                        developer {
                            id = 'jk-5'
                            name = 'jk-5'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }else{
        logger.lifecycle('Deploying to mavenLocal')
        repositories{
            mavenLocal()
        }
    }
}
